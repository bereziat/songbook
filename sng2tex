#! /usr/bin/perl -lw
# (c) 2015 D.Bereziat

use strict;

my $numsong = 1;

# LaTeX header
my ($title,$authors,$titlepage,$index);

for (@ARGV) {
  do {
    print STDERR "Usage is: sng2tex [-t][-i] FILE FILE ...";
    exit 1;
  } if /^-h/;
  $titlepage = 1 if /^-t$/;
  $index = 1 if /^-i$/;
}

print "\\documentclass{article}";
print "\\usepackage{fullpage,hyperref}";
print "\\usepackage[chorded]{songs}";
print "\\usepackage[utf8]{inputenc}";
print "\\noversenumbers";
print "\\begin{document}";
print "\\title{The Famous Boby Boeuf Songbook}";
print "\\date{May 2015}";
print "\\maketitle" if $titlepage;
print "\\include{index}" if $index;
print "\\begin{songs}{}";

# Index of songs
open IND, ">index.tex";
print IND "\\begin{center} \\huge \\bf Index of songs \\end{center}\n\\begin{enumerate}";

for my $file (@ARGV) {
  next if $file =~ /^-/;

  open (F, "<$file") ;

  do { $_=<F>} while (/^%%/) ;
  $title = $_;
  chomp $title;
  do { $_=<F>} while (/^%%/) ;
  $authors = $_;
  chomp $authors;

  print "\\beginsong{$title}[by={$authors}]\\label{song$numsong}";

  # index song:
  print IND "\\item [] \\large $title \\hfill page \\pageref{song$numsong}";

  $numsong ++;

  # read chords definition
  while ( <F>) {
    chomp;
    # Skips comments
    next if /^%%/;
    # Chord definitions
    if (/^%chord +(\S+) +(\S+)/) {
      my $name = $1;
      my $def = $2;
      $name =~ s/b/\&/;
      print "\\gtab{$name}{$def}";
      next;
    }
    last;
  }

  print "\\beginverse";

  while( <F>) {
    chomp;
    # Comments
    next if /^%%/;

    # Annotations
    do { print "\\vspace*{.3cm}\\textnote{$1}"; next} if /^%\s+(.*)/ ;

    # Chords
    s/\[/\\[/g;

    # Bemol in chords (convert b to &)
    while (s/(\[[^\]])b(?=.*\])/$1\&/) {};

    # capture words group / isolated letter(s) with _ marker
    while (s/\](([^_\[]+)_)/\]\{$2\}/) {};

    print ;
  }
  close F;

  print "\\endverse";
  print "\\endsong";
  print "%%%%%%%%%%%%%%%"
}

print "\\end{songs}";
print "\\end{document}";

print IND "\\end{enumerate}";
close IND;
